plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '1.2.4'
}

ext {
    gitHash = getGitHash()
}

private static String getGitHash() {
    def process = 'git rev-parse --short HEAD'.execute()
    process.waitFor()
    return process.exitValue() ? 'unknown' : process.text.trim()
}

group 'tech.ferus.util'
version = '1.0-SNAPSHOT-' + gitHash
archivesBaseName = "BasicSql"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile "org.slf4j:slf4j-api:1.7.21"
    compile "com.google.code.findbugs:jsr305:3.0.2"
    compile "mysql:mysql-connector-java:6.0.5"
    compile "com.mchange:c3p0:0.9.5.2"
}

compileJava {
    options.encoding = 'UTF-8'
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': version,
                'Build-Version': gitHash
        )
    }
}

shadowJar {
    classifier = "shaded"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    failOnError = false
    source = sourceSets.main.allJava
    classpath = configurations.compile
    options.memberLevel = JavadocMemberLevel.PRIVATE
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
    archives shadowJar
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2.1'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

build {
    dependsOn clean
    dependsOn jar
    dependsOn javadocJar
    dependsOn sourcesJar
    dependsOn shadowJar

    jar.mustRunAfter clean
    javadocJar.mustRunAfter jar
    sourcesJar.mustRunAfter javadocJar
    shadowJar.mustRunAfter sourcesJar
}